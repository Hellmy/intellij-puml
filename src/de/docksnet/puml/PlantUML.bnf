
{
  parserClass="de.docksnet.puml.parser.PumlParser"
  stubParserClass="de.docksnet.puml.GeneratedParserUtilBase"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Puml"
  psiImplClassSuffix="Impl"
  psiPackage="de.docksnet.puml.psi"
  psiImplPackage="de.docksnet.puml.psi.impl"

  elementTypeHolderClass="de.docksnet.puml.psi.PumlTypes"
  elementTypeClass="de.docksnet.puml.psi.PumlElementType"
  tokenTypeClass="de.docksnet.puml.psi.PumlTokenType"

  // operators
  COLON             = ":"
  PAR_LEFT          = "("
  PAR_RIGHT         = ")"
  STEREOTYPE_START  = "<<"
  STEREOTYPE_END    = ">>"
}

pumlFile ::= UML_START entry_* UML_END

private entry_ ::= (actorStatement|usecaseStatement|COMMENT|EOL)

actorStatement ::= actorDef [AS ID] [stereotype] EOL {pin=1}
actorDef ::= (ACTOR ID | ':' ID ':' | ACTOR ':' ID ':')

usecaseStatement ::= usecaseDef [AS usecaseAlias] [stereotype] EOL {pin=1}
usecaseDef ::= (USECASE ID | '(' ID ')' | USECASE '(' ID ')')
usecaseAlias ::= ('(' ID ')' | ID)

stereotype ::= '<<' STRING '>>'
